import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    // Apply the application plugin to add support for building a CLI application.
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

repositories {
    jcenter()
}

sourceCompatibility = '14';

if(project.version == "unspecified"){
  String now = new Date().format( 'MMM-dd-yyyy_HH-mm-ss' )
  project.version = "1.0.0-${now}-SNAPSHOT"
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-logging:2.3.3.RELEASE'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

application {
    // Define the main class for the application.
    mainClassName = 'com.github.jscancella.App'
}

javafx {
    version = "14"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

processResources {
	def formattedDate = LocalDate.now().format(java.time.format.DateTimeFormatter.ofLocalizedDate(java.time.format.FormatStyle.MEDIUM))
    filesMatching('**/MessageBundle.properties') {
    	filter(ReplaceTokens, tokens:["VERSION": project.version.toString(), "RELEASED": formattedDate.toString()])
    }
}

test {
    useJUnitPlatform()
}